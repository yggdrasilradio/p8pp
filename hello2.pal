	CR=15
	LF=12

	*200
        cla cll
	tad (hello1	/ "HELLO WORLD!"
	jms print	/ display on terminal
	tad (hello2	/ "HOW'S IT GOING?"
	jms print	/ display on terminal
        jmp i [7600	/ return to OS8 monitor

/ Print a NULL-terminated string
print,	0
        dca ptr

	/ Get next two characters
print0,	tad i ptr
        sna
        jmp print1
	dca word

	/ Print left 6 bits
	tad word
	bsw
	and (77
	sna
	jmp print1
	jms prchar

	/ Print right 6 bits
	tad word
	and (77
	sna
	jmp print1
        jms prchar

	isz ptr
        jmp print0

	/ Print CRLF
print1,	tad (CR
	jms tty
	tad (LF
	jms tty
	jmp i print

ptr,	0
word,	0

/ Convert single character to ASCII and display on terminal
prchar,   0

	/ convert 6-bit to ASCII
        tad (40
        and (77
        tad (40

	/ display character
	jms tty

        jmp i prchar

/ Display single character on terminal
tty,	0
        tls
        tsf
        jmp .-1
	cla
	jmp i tty

hello1,	text      /HELLO, WORLD!/
hello2,	text      /HOW'S IT GOING?/
