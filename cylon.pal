
	*200

	/ Set up the bit pattern to rotate
	cla cll
	mql
	tad (7

	/ Rotate pattern back and forth through AC
loop,	szl
	jms right
	rar
	szl
	jms left
	ral
	jms wait
hook,	ral
	jmp loop

	/ Rotate bits left
left,	0
	dca ac
	tad lop
	dca hook
	tad ac
	jmp i left

lop,	ral

	/ Rotate bits right
right,	0
	dca ac
	tad rop
	dca hook
	tad ac
	jmp i right

rop,	rar

ac,	0

	/ Wait a bit
wait,	0
	dca ac		/ save AC
	tad delay	/ set up delay counter
	dca count2
	tad ac		/ restore AC
wait0,	isz count1
	jmp wait0
	jms keyin	/ check for keystroke
	isz count2
	jmp wait0
	jmp i wait

	/ Return to OS8 monitor on any key
keyin,	0
	ksf		/ key pressed?
	jmp i keyin	/ no
	krb		/ yes
	jmp i [7600	/ return to OS8

count1,	0
count2,	0

	decimal

delay,	-100
